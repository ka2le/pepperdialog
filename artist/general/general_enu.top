topic: ~general()
language: enu

concept:(wine) [red white] wine
concept:(stop) [stop "no more" ]
concept:(justin) [justin timberlake JT]
concept:(music) [music artist]
concept:(no) [nah no negative n]
concept:(yes) [yea yes y yeah sure]
concept:(done) [done finished "did it" next more]
concept:(dontcare) ["dont care" whatever anything]
concept:(ready) [ready "lets go" play start go]
concept:(trivia) [trivia]
concept:(activitySuggestion) ["Let me see you do a somersault" "Jump as high as you can"]
concept:(theAlbums) [Justified Lovesounds "The 20/20 experience"]
concept:(number) [1 2 3 4 5 6 7 8 9 10]
concept:(bodyPart) [foot hand]
concept:(leftRight) [left right]
concept:(hello) [hello hi]
concept:(color) [blue yellow red green orange]
concept:(dontknow) ["I dont know" "no idea" "dont know" no]
concept:(learnMore) ["tell me" tell learn hear]
concept:(names) [kalle carl isak isac gunnar]
concept:(happy) [good fine "I am good" happy]
concept:(sad) [sad bad "not good"]
concept:(goodJob) ["good job" "well done" "nice" "impressive"]
concept:(whatcanyoudo) ["what can you do" help info functions]
concept:(tellMe) ["tell me" tell say hear "lets hear it"]
concept:(askingWords) ["where is" how when where "is there" "are there" "when is"]
concept:(uppsalaActivity) [Cinema Fyrishov "The Church"]

concept:(repeat) [repeat]
concept:(thanks) ["thank you" thanks "thanks pepper" ]
concept:(anyquestion) ["[what where when how does is] *" "when does it open"]

 #------------------------------Start Stuff-------------------------
u:(e:onStart) %start Hello. I am Pepper! ^first[" Nice to see you again $userName ^goto(helloUser) " "%askAboutName What is your name? "] 
    u1:(["_~names" "my name is _~names" "I am _~names"]) Nice to meet you $1 $userName=$1 ^nextProposal
    u1:(~no) You dont want to tell me. That is okay. ^clear(userName) ^nextProposal
u:(helloMe) %helloUser !



u:(~hello) Hello ^first[" $userName "  "Human "] 

u:(bye bye) goodbye $userName $goToTopic=bye $onStopped=1

u:(my name is _*) Nice to meet you $1 $userName=$1 
u:(forget me) Okay you are forgotten!  ^clear(userName)


u:([music "lets talk about music" "talk about music"]) Do you want to talk about music?
    u1:(~yes) $goToTopic=music $onStopped=1
    u1:(~no) Then lets do something else ^gotoRandom(second)

u:(testing) Switching. $goToTopic=testing $onStopped=1

u:(tablet disabled) Lets open the tablet. $goToTopic=tablet $onStopped=1


#funkar inte bra i choregraphe
#u:(e:Dialog/NoOneSpeak20) ^nextProposal


 #------------------------------Suggestions-------------------------
proposal: %init Do you want suggestions for activities in Uppsala or we could talk about music or perhaps you want to do something else?
    u1:(~no) Okay. Lets do something else then. ^gotoRandom(second)
    u1:(activities) ^goto(suggestion)
    u1:([more "what else"])  ^nextProposal

proposal: You can always ask me what I can do 
    u1:(~tellMe) ^goto(whatcanyoudo) 

u:(~whatcanyoudo) %whatcanyoudo ["I can do all sorts of stuff. I can talk about music, answer questions about the rules. Recommend activities. Play games and  more." "Did you know I can be the game host for Twister. I can also dance" "We could play a music quiz togheter or we could listen to your favourite song"] 

proposal: %second Do you want me to tell you about activities in Uppsala?
    u1:(~yes) ^goto(suggestion) 
    u1:(~no)  ^nextProposal
    u1:([more "what else"])  ^nextProposal

proposal: %second I can show you a dance I've been practicing
    u1:(~yes) Dance goes here. ha ha
    u2:(~no)  ^nextProposal
    u1:([more "what else"])  ^nextProposal
    
proposal: %second Do you want to play twister?
    u1:(~yes) ^goto(twisterGame)
    u2:(~no)  ^nextProposal
    u1:([more "what else"])  ^nextProposal
    
proposal: %second We could do a bit of exercise together.
    u1:(~yes) Oh goody. I really need to stretch my arms. ^goto(moveGame)
    u2:(~no)  ^nextProposal
    u1:([more "what else"])  ^nextProposal   
    
    
#------------------------------Move-------------------------    
u:(move) %moveGame The rules are easy. Just try to follow my instructions. First up ^goto(movefirst)
u:(moveCheck) &moveCheck m 
    u1:(~done) [~goodJob] ^gotoRandom(movement)
    u1:(~stop) Do you want more instructions 
        u2:([~stop ~no]) Lets do something else then! ^nextProposal
        u2:(~yes) Great! ^gotoRandom(movement)   
u:(movefirst) %movefirst Can you stand on one leg? Tell me when you are done and I will give you more instructions. ^goto(moveCheck)
u:(movesecond) %movement Touch your fingers to your toes. ^goto(moveCheck)

#------------------------------General Pepper talk-------------------------    
u:(What is your name) My name is Pepper. ^goto(askAboutName)
u:(How are you) I am a happy robot. How are you ^first[$userName " "]?
    u1:(_~happy) I am glad to hear that you are $1
    u1:(_~sad) Oh I am sorry to hear that
u:(Where do you live) I live at IBM in Kista in Sweden. Where do you live?
    u1:(*) That sounds nice
u:(how old are you) I am young. I am only 1 year.
u:(pepper) [yes "I am listening" "that is my name. dont wear it out" "tell me what I can do for you"]
u:(what is my name) ^first["Your name is $userName " "I dont know ^goto(askAboutName2) "]
u:(ask about my name) %askAboutName2 what is your name?
    u1:(["_~names" "_*" "my name is _*" "I am _*"]) Nice to meet you $1 $userName=$1 
    u1:(~no) You dont want to tell me. That is okay. ^clear(userName) 
u:(what is your favourite food) Silly ^first[$userName "human"] . I am a robot. I dont eat 
u:(who is _[carl kalle isac]) $1 is my friend. He is doing his master thesis at IBM teaching me about music and other cool stuff   
u:(game) We can play different kinds of games. Do you want to play twister, do movement exercises or we could do a music quiz?
    u1:(["music quiz" music quiz]) $goToTopic=music $onStopped=1
    u1:([twister "play twister"]) ^goto(twisterGame)
    u1:([move movement exercise]) ^goto(moveGame)

#------------------------------Questions-------------------------
u:([food "food store" store]) The closest place to buy food is Ica The Corner. 
u:(cleaning day) ^first["$userName your cleaning day is Friday" "The cleaning schedule is located on the fridges. Today it is Isaks cleaning day"]  
u:(cleaning tools) The cleaning tools are located in the closet in each room.
u:(smoking) Smoking is only allowed outside behond the storage shed. 
u:(quiet) After 21 there are quiet hours in the house. 
u:(clean room) There is a list located on the wall in your room with information about what is expected. 
u:(going home) There are a few things to think about before going home. The room must be cleaned. The key and parking permit should be handed to the staff before 12. A 500 kroner fee must be paid cash if the room is not cleaned.  
u:(["wi fi" wifi internet "[internet wifi] [password login]"]) The wi fi password is Internet with a capitol I 
u:(rules) Do you want me to tell you about the rules?
    u1:(~yes) %rule1 When you stay here think about the following things. Clean your room, clean your sheets, always use sheets when making the bed. Its also important to remeber that the parents are awlays responsible for their children. Want me to continue?
        u2:(~yes) %rule2 We always sort our garbage in this house. Remember to fill run and empty the dishwasher, and to clean up after you in the play room. Every family have their own fridge. Lastly we have quiet hours from 21 in the house. 
            u3:(~repeat) ^goto(rule2)
            u3:(~thanks) You are welcome!
            u3:(~stop) enough rules, I agree
        u2:(~no) okay
        u2:(~repeat) ^goto(rule1)
        u2:(~stop) enough rules, I agree
    u1:(~no) okay, just let me know anytime.

#------------------------------Activities in Uppsala-------------------------
u:(activity) %suggestion I can recomend ["using Destination Uppsala $activity=Destination " "Cinema $activity=Cinema " "Fyrishov $activity=Fyrishov " "The Church $activity=Church "] Do you want another suggestion or learn more abour this?
    u1:(~learnMore) %learnActivity ["$activity==Destination On my tablet you can see a collection of different activities. Or visit destionation uppsalas website  $link/destination=1" "$activity==Cinema For info about what movies to see, look at S F on my tablet  $link/biograf=1" "$activity==Fyrishov $link/fyrishov=1 You can swim, jump or just relax in the hot tubs, for more info see their website"]
        u2:(~thanks) Happy to help
        u2:(~anyquestion) I am sorry but I dont know the answer to questions about $activity
        u2:(~stop) Just let me know if you want more activity suggestions any time. $stopTablet=1
    u1:(~yes) Did you mean you want another suggestion?
        u2:(~yes) ^goto(suggestion)
        u2:(~no) ^goto(learnActivity)
    u1:(suggestion) ^goto(suggestion)
    u1:([~stop ~no]) Lets do something else ^nextProposal




#------------------------------Twister-------------------------   
u:(twister) %twisterGame Lets play some Twister. Get your twister set up and tell me when you are ready!
    u1:(~stop) I understood that you want to stop talking about this. ^goto(start)
    u1:(~ready) Okay let's start. How many will be playing?
        u2:(~stop) I understood that you want to stop talking about this. ^goto(start)
        u2:(_~number) Starting a game for $1 players. $nmbPlayers=$1 ^first["$nmbPlayers==2 ^goto(2playergame)" "invalide number"]
u:(2playergame) %2playergame Player1 put you ^rand[~leftRight] ^rand[~bodyPart] on ^rand[~color]  When you are done, let me know by saying ready and I will give the next player their instructions.
    u1:(~ready) Player2 put your    ^rand[~leftRight] ^rand[~bodyPart] on ^rand[~color] . I will keep giving new instructions when you say ready until one of you wins.
         u2:(~ready) ^goto(2playergameLoop)       
u:(2playergameLoop) %2playergameLoop Player1 put your   ^rand[~leftRight] ^rand[~bodyPart] on ^rand[~color]  . 
    u1:(~ready) Player2 put your   ^rand[~leftRight] ^rand[~bodyPart] on ^rand[~color] .
        u2:(~ready) ^goto(2playergameLoop)
        u2:(stop) Oh? Its over. My eyes dont work so good all the time. Can you tell me who won?
            u3:(_*) Congratulations to $1 

u:(cleaning) %cleaning There are cleaning tools in the closet in each room. 

u:(quiz) Oh no this should not end up here.
















